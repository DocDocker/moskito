#Использовать asserts
#Использовать ".."

Перем Объект;

&Тест
Процедура ТестДолжен_ПроверитьПроцедуру() Экспорт

	МокОбъект = Мок.Получить(Тип("Структура"));
	МокОбъект.Когда()
			 .Вставить("_ТретьеПлохоеИмя")
			 .ТогдаВыбрасываетИсключение(Новый ИнформацияОбОшибке("Недопустимыое имя ключа", Новый Структура)); 
			 
	Ожидаем.Что(МокОбъект)
		   .Метод("Вставить", НовыйМассив("_ТретьеПлохоеИмя"))
		   .ВыбрасываетИсключение("Недопустимыое имя ключа"); 
	   
КонецПроцедуры	

&Тест
Процедура ТестДолжен_ПроверитьВызовМетода() Экспорт

	МокОбъект = Мок.Получить(Тип("Структура"));
	
	Ожидаем.Что(МокОбъект.Свойство(Новый Структура)).Равно(NULL);

	МокОбъект.Когда()
			 .Свойство(1)
			 .ТогдаВыбрасываетИсключение("Первое исключение");

	МокОбъект.Когда()
			 .Свойство(1)
			 .ТогдаВыбрасываетИсключение("Текстовое описание исключения");

	МокОбъект.Когда()
			 .Свойство("_ВтороеПлохоеИмя")
			 .ТогдаВыбрасываетИсключение(Новый ИнформацияОбОшибке("Краткое Описание", Новый Структура));
	
	Ожидаем.Что(МокОбъект.Свойство(0)).Равно(NULL);
	Ожидаем.Что(МокОбъект.Свойство(Новый Массив)).Равно(NULL);
	Ожидаем.Что(МокОбъект.Свойство("Другая строка")).Равно(NULL);

	Ожидаем.Что(МокОбъект)
		   .Метод("Свойство" , НовыйМассив(1))
		   .ВыбрасываетИсключение("Текстовое описание исключения");

	Ожидаем.Что(МокОбъект)
		   .Метод("Свойство" , НовыйМассив("_ВтороеПлохоеИмя"))
		   .ВыбрасываетИсключение("Краткое Описание");

	МокОбъект.Когда()
	         .Свойство(Матчеры.ЛюбаяСтрока())
			 .ТогдаВыбрасываетИсключение("Строка");
	
	Ожидаем.Что(МокОбъект)
			.Метод("Свойство" , НовыйМассив("ПростоСтрока"))
			.ВыбрасываетИсключение("Строка");
	

	ИнформацияОбОшибке = Новый ИнформацияОбОшибке("ЛюбоеЗначение", Новый Структура("Параметр", "ЗначениеПараметра"));
	МокОбъект.Когда()
	         .Свойство(Матчеры.ЛюбоеЗначение())
	         .ТогдаВыбрасываетИсключение(ИнформацияОбОшибке);

    ИсключениеВызывалось = Ложь;		 
	Попытка		 
		МокОбъект.Свойство(Новый Массив);
	Исключение
		ИсключениеВызывалось = Истина;
		Ошибка = ИнформацияОбОшибке();
	КонецПопытки;	

	Ожидаем.Что(ИсключениеВызывалось).ЭтоИстина();
	Ожидаем.Что(Ошибка.Параметры.Параметр).Равно("ЗначениеПараметра");
	Ожидаем.Что(Ошибка.Описание).Равно("ЛюбоеЗначение");
	
КонецПроцедуры	

Функция НовыйМассив(ВхПараметр) 
 
	Массив = Новый Массив; 
	Массив.Добавить(ВхПараметр); 
   
	Возврат Массив; 
   
  КонецФункции  