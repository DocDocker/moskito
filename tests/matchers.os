#Использовать asserts
#Использовать notify
#Использовать delegate
#Использовать ".."

Перем Объект;


Процедура ПередЗапускомТеста() Экспорт
	Объект = Мок.Получить(Тип("Структура"));
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьМатчерЧисла() Экспорт
	Объект.Когда().Свойство(Матчеры.ЛюбоеЧисло()).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство(0)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(NULL);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьМатчерСтроки() Экспорт
	Объект.Когда().Свойство(Матчеры.ЛюбаяСтрока()).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(Истина);
	Ожидаем.Что(Объект.Свойство(0)).Равно(NULL);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьВсеядныйМатчер() Экспорт
	Объект.Когда().Свойство(Матчеры.ЛюбоеЗначение()).ТогдаВозвращает(Истина);

	Ожидаем.Что(Объект.Свойство(0)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(Истина);
	Ожидаем.Что(Объект.Свойство(Неопределено)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство(Ложь)).Равно(Истина);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьКомбинациюМатчеров() Экспорт
	Объект.Когда().Свойство(Матчеры.ЛюбоеЧисло()).ТогдаВозвращает(Истина);
	Объект.Когда().Свойство(Матчеры.ЛюбаяСтрока()).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство(0)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(Истина);
	Ожидаем.Что(Объект.Свойство(Неопределено)).Равно(NULL);
	Ожидаем.Что(Объект.Свойство(Ложь)).Равно(NULL);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьКастомныйМатчер() Экспорт
	КастомныйМатчер = Новый Матчер(ЭтотОбъект, "БольшеТрех");
	Объект.Когда().Свойство(КастомныйМатчер).ТогдаВозвращает(Истина);

	Ожидаем.Что(Объект.Свойство(0)).Равно(NULL);
	Ожидаем.Что(Объект.Свойство(3)).Равно(NULL);
	Ожидаем.Что(Объект.Свойство(4)).Равно(Истина);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьМатчерСПараметрами() Экспорт
	Объект.Когда().Свойство(Матчеры.ИмеетТип("Число")).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство(0)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство(1)).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(NULL);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьРаботуДелегата() Экспорт
	КастомныйДелегат = Новый Делегат(ЭтотОбъект, "БольшеТрех");
	Объект.Когда().Свойство(КастомныйДелегат).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство(0)).Равно(NULL);
	Ожидаем.Что(Объект.Свойство(3)).Равно(NULL);
	Ожидаем.Что(Объект.Свойство(4)).Равно(NULL);

	Ожидаем.Что(Объект.Свойство(КастомныйДелегат)).Равно(Истина);
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьРаботуVerify() Экспорт
	
	Объект.Свойство("Строка");
	Объект.ПроверитьЧтоВызывалсяМетод().Свойство(Матчеры.ЛюбаяСтрока());

	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить(Матчеры.ЛюбоеЧисло());
	Ожидаем.Что(Объект.ПроверитьЧтоВызывалсяМетод()).Метод("Свойство", ПараметрыМетода).ВыбрасываетИсключение("Свойство");
	
	Объект.Свойство(0);
	Объект.ПроверитьЧтоВызывалсяМетод().Свойство(Матчеры.ЛюбоеЧисло());

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьПересечениеРаботыМетодов() Экспорт
	Объект.Когда().Свойство(Матчеры.ЛюбоеЧисло()).ТогдаВозвращает(Истина);
	Объект.Свойство(0);
	Объект.ПроверитьЧтоВызывалсяМетод().Свойство(Матчеры.ЛюбоеЧисло());
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьМатчерСоответствуетВыражению() Экспорт
	Объект.Когда().Свойство(Матчеры.СоответствуетВыражению("\d+")).ТогдаВозвращает(Истина);
	
	Ожидаем.Что(Объект.Свойство("0")).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("123")).Равно(Истина);
	Ожидаем.Что(Объект.Свойство("Строка")).Равно(NULL);
КонецПроцедуры


Функция БольшеТрех(Знач Параметр = Неопределено) Экспорт
	Возврат Параметр > 3;
КонецФункции
