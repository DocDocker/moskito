#Использовать strings

Функция Получить(Знач ИсходныйОбъект) Экспорт

	Рефлектор = Новый Рефлектор;
	Методы = Рефлектор.ПолучитьТаблицуМетодов(ИсходныйОбъект);
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(ИсходныйОбъект);

	ФейковыйОбъектСтрокой = "";

	ШаблонПеременной = "Перем %1 Экспорт;" + Символы.ПС;
	Для Каждого Свойство Из Свойства Цикл
		ФейковыйОбъектСтрокой = ФейковыйОбъектСтрокой + СтрШаблон(ШаблонПеременной, Свойство.Имя); 
	КонецЦикла;

	ШаблонМетода = 
	"%1 %2(%3) Экспорт
	|	Возврат %4;
	|%5" + Символы.ПС + Символы.ПС;

	Для Каждого Метод Из Методы Цикл
		СтрокаПараметров = "";
		Для сч = 1 По Метод.КоличествоПараметров Цикл
			СтрокаПараметров = СтрокаПараметров + "Парам" + сч + ",";
		КонецЦикла;

		Если ЗначениеЗаполнено(СтрокаПараметров) Тогда
			СтроковыеФункции.УдалитьПоследнийСимволВСтроке(СтрокаПараметров);
		КонецЕсли;

		ТипНачалоМетода = ?(Метод.ЭтоФункция, "Функция", "Процедура");
		ТипКонецМетода = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
		ВозвращаемоеЗначение = ?(Метод.ЭтоФункция, "0", "");

		ФейковыйОбъектСтрокой = ФейковыйОбъектСтрокой + СтрШаблон(
			ШаблонМетода, 
			ТипНачалоМетода, 
			Метод.Имя,
			СтрокаПараметров,
			ВозвращаемоеЗначение,
			ТипКонецМетода
		);
	КонецЦикла;

	ФейковыйОбъект = ЗагрузитьСценарийИзСтроки(ФейковыйОбъектСтрокой);

	Возврат ФейковыйОбъект;

КонецФункции