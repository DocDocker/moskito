#Использовать strings

Функция Получить(Знач ИсходныйОбъект) Экспорт

	Рефлектор = Новый Рефлектор;
	Методы = Рефлектор.ПолучитьТаблицуМетодов(ИсходныйОбъект);
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(ИсходныйОбъект);

	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Классы", "МокОбъектСлужебный.os");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ФейковыйОбъектСтрокой = ТекстовыйДокумент.ПолучитьТекст();

	ОбластьПеременных = "";
	ШаблонПеременной = "Перем %1 Экспорт;" + Символы.ПС;
	Для Каждого Свойство Из Свойства Цикл
		ОбластьПеременных = ОбластьПеременных + СтрШаблон(ШаблонПеременной, Свойство.Имя); 
	КонецЦикла;

	ОбластьМетодов = "";
	ШаблонМетода = 
	"%1 %2(%3) Экспорт
	|	Возврат %4;
	|%5" + Символы.ПС + Символы.ПС;

	Для Каждого Метод Из Методы Цикл
		СтрокаПараметров = "";
		Для сч = 1 По Метод.КоличествоПараметров Цикл
			СтрокаПараметров = СтрокаПараметров + "Парам" + сч + ",";
		КонецЦикла;

		Если ЗначениеЗаполнено(СтрокаПараметров) Тогда
			СтроковыеФункции.УдалитьПоследнийСимволВСтроке(СтрокаПараметров);
		КонецЕсли;

		ТипНачалоМетода = ?(Метод.ЭтоФункция, "Функция", "Процедура");
		ТипКонецМетода = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
		ВозвращаемоеЗначение = ?(Метод.ЭтоФункция, "0", "");

		ОбластьМетодов = ОбластьМетодов + СтрШаблон(
			ШаблонМетода, 
			ТипНачалоМетода, 
			Метод.Имя,
			СтрокаПараметров,
			ВозвращаемоеЗначение,
			ТипКонецМетода
		);
	КонецЦикла;

	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область переменных}", ОбластьПеременных);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область методов}", ОбластьМетодов);
	
	ФейковыйОбъект = ЗагрузитьСценарийИзСтроки(ФейковыйОбъектСтрокой);

	Возврат ФейковыйОбъект;

КонецФункции