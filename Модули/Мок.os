#Использовать strings

Функция Получить(Знач ИсходныйОбъект) Экспорт

	Рефлектор = Новый Рефлектор;
	Методы = Рефлектор.ПолучитьТаблицуМетодов(ИсходныйОбъект);
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(ИсходныйОбъект);

	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "МокОбъектСлужебный.os");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ФейковыйОбъектСтрокой = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;

	ОбластьПеременных = "";
	ШаблонПеременной = "Перем %1 Экспорт;" + Символы.ПС;
	Для Каждого Свойство Из Свойства Цикл
		ОбластьПеременных = ОбластьПеременных + СтрШаблон(ШаблонПеременной, Свойство.Имя); 
	КонецЦикла;

	ОбластьМетодов = "";

	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "ШаблонМокМетода.os_template");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ШаблонМетода = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;

	Для Каждого Метод Из Методы Цикл
		СтрокаПараметров = "";
		Для сч = 1 По Метод.КоличествоПараметров Цикл
			СтрокаПараметров = СтрокаПараметров + "Парам" + сч + ",";
		КонецЦикла;

		Если ЗначениеЗаполнено(СтрокаПараметров) Тогда
			СтроковыеФункции.УдалитьПоследнийСимволВСтроке(СтрокаПараметров);
		КонецЕсли;
		
		// TODO: Заполнить Мок_СтруктураПараметрыПроцедуры

		ТипНачалоМетода = ?(Метод.ЭтоФункция, "Функция", "Процедура");
		ТипКонецМетода = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
		ВозвращаемоеЗначение = ?(Метод.ЭтоФункция, "NULL", "");

		НовыйМетод = ШаблонМетода;
		НовыйМетод = СтрЗаменить(НовыйМетод, "Функция", ТипНачалоМетода);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ИмяФункции", Метод.Имя);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ПараметрыПроцедуры", СтрокаПараметров);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ВозвращаемоеИзМетодаЗначение", ВозвращаемоеЗначение);
		НовыйМетод = СтрЗаменить(НовыйМетод, "КонецФункции", ТипКонецМетода);

		ОбластьМетодов = ОбластьМетодов + НовыйМетод;
	КонецЦикла;

	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область переменных}", ОбластьПеременных);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область методов}", ОбластьМетодов);
	
	ФейковыйОбъект = ЗагрузитьСценарийИзСтроки(ФейковыйОбъектСтрокой);

	Возврат ФейковыйОбъект;

КонецФункции