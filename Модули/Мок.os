#Использовать strings

Функция Получить(Знач ИсходныйОбъект) Экспорт
	
	ФейковыйОбъект = СоздатьМок(ИсходныйОбъект);
	Возврат ФейковыйОбъект;

КонецФункции

Функция Следить(Знач ИсходныйОбъект) Экспорт
	ФейковыйОбъект = СоздатьМок(ИсходныйОбъект, Истина);
	Возврат ФейковыйОбъект;
КонецФункции

Функция СоздатьМок(Знач ИсходныйОбъект, Знач ЭтоШпион = Ложь)

	Сообщить("Тип исходного объекта: " + ТипЗнч(ИсходныйОбъект));
	Сообщить("Представление исходного объекта: " + ИсходныйОбъект);
	
	Рефлектор = Новый Рефлектор;
	Методы = Рефлектор.ПолучитьТаблицуМетодов(ИсходныйОбъект);
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(ИсходныйОбъект);
	
	Сообщить("Количество свойств: " + Свойства.Количество());
	Сообщить("Количество методов: " + Методы.Количество());
	
	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "МокОбъектСлужебный.os");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ФейковыйОбъектСтрокой = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;
	
	ОбластьПеременных = "";
	ШаблонПеременной = "Перем %1 Экспорт;" + Символы.ПС;
	Для Каждого Свойство Из Свойства Цикл
		ОбластьПеременных = ОбластьПеременных + СтрШаблон(ШаблонПеременной, Свойство.Имя); 
	КонецЦикла;
	
	ОбластьМетодов = "";
	
	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "ШаблонМокМетода.os_template");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ШаблонМетода = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;
		
	Для Каждого Метод Из Методы Цикл
		СтрокаПараметров = "";
		ШаблонОписанияПараметра = "%1 = Неопределено,";
		Строка_Мок_СтруктураПараметрыПроцедуры = "";
		
		Для сч = 1 По Метод.КоличествоПараметров Цикл
			ИмяПараметра = "Парам" + сч;
			СтрокаПараметров = СтрокаПараметров + СтрШаблон(ШаблонОписанияПараметра, ИмяПараметра);
			Строка_Мок_СтруктураПараметрыПроцедуры = Строка_Мок_СтруктураПараметрыПроцедуры + Символы.Таб + СтрШаблон(
				"Мок_СтруктураПараметрыПроцедуры.Вставить(""%1"", %1);",
				ИмяПараметра
			) + Символы.ПС;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СтрокаПараметров) Тогда
			СтроковыеФункции.УдалитьПоследнийСимволВСтроке(СтрокаПараметров);
		КонецЕсли;
		
		ТипНачалоМетода = ?(Метод.ЭтоФункция, "Функция", "Процедура");
		ТипКонецМетода = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
		ВозвращаемоеЗначение = ?(Метод.ЭтоФункция, "Возврат Мок_ВозвращаемоеИзМетодаЗначение;", "");
		ТипМетода = "Мок_ТипМетода = " + Формат(Метод.ЭтоФункция, "БЛ=Ложь; БИ=Истина") + ";";
		
		НовыйМетод = ШаблонМетода;
		НовыйМетод = СтрЗаменить(НовыйМетод, "Функция", ТипНачалоМетода);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ИмяФункции", Метод.Имя);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Мок_ТипМетода}", ТипМетода);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ПараметрыПроцедуры", СтрокаПараметров);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Возвращаемое значение}", ВозвращаемоеЗначение);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Мок_СтруктураПараметрыПроцедуры}", Строка_Мок_СтруктураПараметрыПроцедуры);
		НовыйМетод = СтрЗаменить(НовыйМетод, "КонецФункции", ТипКонецМетода);
		НовыйМетод = НовыйМетод + Символы.ПС;

		ОбластьМетодов = ОбластьМетодов + НовыйМетод;
	КонецЦикла;
	
	ОбластьИнициализации = "";
	ОбластьИнициализации = ОбластьИнициализации + "Мок_ЭтоШпион = " + Формат(ЭтоШпион, "БЛ=Ложь; БИ=Истина") + ";" + Символы.ПС;

	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область переменных}", ОбластьПеременных);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область методов}", ОбластьМетодов);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Инициализация}", ОбластьИнициализации);

	Сообщить(ФейковыйОбъектСтрокой);

	ФейковыйОбъект = ЗагрузитьСценарийИзСтроки(ФейковыйОбъектСтрокой);
	ФейковыйОбъект.Мок_УстановитьМокируемыйОбъект(ИсходныйОбъект);

	ЗаполнитьЗначенияСвойств(ФейковыйОбъект, ИсходныйОбъект);
	
	Возврат ФейковыйОбъект;

КонецФункции
