#Использовать strings
#Использовать logos

Перем Лог;

Функция Получить(Знач ИсходныйОбъект) Экспорт
	
	ФейковыйОбъект = СоздатьМок(ИсходныйОбъект);
	Возврат ФейковыйОбъект;

КонецФункции

Функция Следить(Знач ИсходныйОбъект) Экспорт
	ФейковыйОбъект = СоздатьМок(ИсходныйОбъект, Истина);
	Возврат ФейковыйОбъект;
КонецФункции

Функция СоздатьМок(Знач ИсходныйОбъект, Знач ЭтоШпион = Ложь)

	Лог.Отладка("Тип исходного объекта: %1", ТипЗнч(ИсходныйОбъект));
	Лог.Отладка("Представление исходного объекта: %1", ИсходныйОбъект);

	РежимСозданияИзТипа = ТипЗнч(ИсходныйОбъект) = Тип("Тип");

	Рефлектор = Новый Рефлектор;
	Методы = Рефлектор.ПолучитьТаблицуМетодов(ИсходныйОбъект);
	Свойства = Рефлектор.ПолучитьТаблицуСвойств(ИсходныйОбъект);
	
	Лог.Отладка("Количество свойств: %1", Свойства.Количество());
	Лог.Отладка("Количество методов: %1", Методы.Количество());
	
	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "МокОбъектСлужебный.os");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ФейковыйОбъектСтрокой = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;
	
	ОбластьПеременных = "";
	ШаблонПеременной = "Перем %1 Экспорт;" + Символы.ПС;
	Для Каждого Свойство Из Свойства Цикл
		ОбластьПеременных = ОбластьПеременных + СтрШаблон(ШаблонПеременной, Свойство.Имя); 
	КонецЦикла;
	
	ОбластьМетодов = "";
	
	ПутьКФейковомуОбъекту = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Модули", "ШаблонМокМетода.os_template");
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьКФейковомуОбъекту, КодировкаТекста.UTF8NoBom);
	ШаблонМетода = ТекстовыйДокумент.ПолучитьТекст();
	ТекстовыйДокумент = Неопределено;
		
	Для Каждого Метод Из Методы Цикл
		СтрокаПараметров = "";
		ШаблонОписанияПараметра = "%1 = Неопределено,";
		Строка_Мок_СтруктураПараметрыПроцедуры = "";
		
		Для сч = 1 По Метод.КоличествоПараметров Цикл
			ИмяПараметра = "Парам" + сч;
			СтрокаПараметров = СтрокаПараметров + СтрШаблон(ШаблонОписанияПараметра, ИмяПараметра);
			Строка_Мок_СтруктураПараметрыПроцедуры = Строка_Мок_СтруктураПараметрыПроцедуры + Символы.Таб + СтрШаблон(
				"Мок_СтруктураПараметрыПроцедуры.Вставить(""%1"", %1);",
				ИмяПараметра
			) + Символы.ПС;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СтрокаПараметров) Тогда
			СтроковыеФункции.УдалитьПоследнийСимволВСтроке(СтрокаПараметров);
		КонецЕсли;
		
		ТипНачалоМетода = ?(Метод.ЭтоФункция, "Функция", "Процедура");
		ТипКонецМетода = ?(Метод.ЭтоФункция, "КонецФункции", "КонецПроцедуры");
		// ВозвращаемоеЗначение = ?(Метод.ЭтоФункция, "Возврат Мок_ВозвращаемоеИзМетодаЗначение;", "");
		ВозвращаемоеЗначение = "Возврат Мок_ВозвращаемоеИзМетодаЗначение;"; 
		ТипМетода = "Мок_ТипМетода = " + Формат(Метод.ЭтоФункция, "БЛ=Ложь; БИ=Истина") + ";";
		
		НовыйМетод = ШаблонМетода;
		// НовыйМетод = СтрЗаменить(НовыйМетод, "Функция", ТипНачалоМетода);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ИмяФункции", Метод.Имя);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Мок_ТипМетода}", ТипМетода);
		НовыйМетод = СтрЗаменить(НовыйМетод, "Мок_ПараметрыПроцедуры", СтрокаПараметров);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Возвращаемое значение}", ВозвращаемоеЗначение);
		НовыйМетод = СтрЗаменить(НовыйМетод, "// {Мок_СтруктураПараметрыПроцедуры}", Строка_Мок_СтруктураПараметрыПроцедуры);
		// НовыйМетод = СтрЗаменить(НовыйМетод, "КонецФункции", ТипКонецМетода);
		НовыйМетод = НовыйМетод + Символы.ПС;

		ОбластьМетодов = ОбластьМетодов + НовыйМетод;
	КонецЦикла;
	
	ОбластьИнициализации = "";
	ОбластьИнициализации = ОбластьИнициализации + "Мок_ЭтоШпион = " + Формат(ЭтоШпион, "БЛ=Ложь; БИ=Истина") + ";" + Символы.ПС;

	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область переменных}", ОбластьПеременных);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Область методов}", ОбластьМетодов);
	ФейковыйОбъектСтрокой = СтрЗаменить(ФейковыйОбъектСтрокой, "// {Инициализация}", ОбластьИнициализации);

	Лог.Отладка(ФейковыйОбъектСтрокой);

	ФейковыйОбъект = ЗагрузитьСценарийИзСтроки(ФейковыйОбъектСтрокой);
	
	Если РежимСозданияИзТипа Тогда
		Попытка 
			ИнстансИсходногоОбъекта = Новый(ИсходныйОбъект);
		Исключение
			Если ЭтоШпион Тогда
				ВызватьИсключение "Невозможно создать объект и данного типа объекта";
			КонецЕсли;
			ИнстансИсходногоОбъекта = Неопределено;
		КонецПопытки;
	Иначе
		ИнстансИсходногоОбъекта = ИсходныйОбъект;
	КонецЕсли;

	ФейковыйОбъект.Мок_УстановитьМокируемыйОбъект(ИнстансИсходногоОбъекта);
	
	Если ИнстансИсходногоОбъекта <> Неопределено Тогда
		ИменаСвойств = Новый Массив; 
		Для Каждого СтрокаСвойства Из Свойства Цикл
			ИменаСвойств.Добавить(СтрокаСвойства.Имя);	
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(ФейковыйОбъект, ИнстансИсходногоОбъекта, СтрСоединить(ИменаСвойств, ","));
	КонецЕсли;

	Возврат ФейковыйОбъект;

КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.moskito");

