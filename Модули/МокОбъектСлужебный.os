// {Область переменных}

Перем Мок_РежимУстановкиВозвращаемогоЗначенияМетода;
Перем Мок_ВозвращаемыеЗначения;
Перем Мок_УстанавливаемоеВозвращаемоеЗначение;

// {Область методов}

Процедура Мок_НачатьУстанавливатьВозвращаемоеЗначение(Знач Мок_ИмяМетода, Знач Мок_ПараметрыПроцедуры)
	
	// Заполним колонки
	Мок_Колонки = Мок_ВозвращаемыеЗначения.Колонки;
	Для Каждого Мок_КлючИЗначение Из Мок_ПараметрыПроцедуры Цикл
		Если Мок_Колонки.Найти(Мок_КлючИЗначение.Ключ) = Неопределено Тогда
			Мок_Колонки.Добавить(Мок_КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;

	// Создадим и сохраним данные для возращаемого значения
	Мок_СтруктураПоиска = Новый Структура(Мок_ПараметрыПроцедуры);
	Мок_СтруктураПоиска.Вставить("ИмяМетода", Мок_ИмяМетода);
	Мок_НайденныеСтроки = Мок_ВозвращаемыеЗначения.НайтиСтроки(Мок_СтруктураПоиска);
	Если Мок_НайденныеСтроки.Количество() = 0 Тогда
		Мок_УстанавливаемоеВозвращаемоеЗначение = Мок_ВозвращаемыеЗначения.Добавить();
		ЗаполнитьЗначенияСвойств(Мок_УстанавливаемоеВозвращаемоеЗначение, Мок_СтруктураПоиска);
	Иначе
		Мок_УстанавливаемоеВозвращаемоеЗначение = Мок_НайденныеСтроки[0];
	КонецЕсли;

КонецПроцедуры

Процедура Мок_СохранитьВозвращаемоеЗначение(Знач Мок_ВозвращаемоеЗначение)
	Мок_УстанавливаемоеВозвращаемоеЗначение.Мок_ВозвращаемоеЗначение = Мок_ВозвращаемоеЗначение;
КонецПроцедуры

Функция Когда() Экспорт

	Мок_РежимУстановкиВозвращаемогоЗначенияМетода = Истина;
	Возврат ЭтотОбъект;

КонецФункции

Процедура ТогдаВозвращает(Знач Мок_ВозвращаемоеЗначение) Экспорт

	Мок_РежимУстановкиВозвращаемогоЗначенияМетода = Ложь;
	Мок_СохранитьВозвращаемоеЗначение(Мок_ВозвращаемоеЗначение);

КонецПроцедуры

Мок_ВозвращаемыеЗначения = Новый ТаблицаЗначений;
Мок_ВозвращаемыеЗначения.Колонки.Добавить("ИмяМетода");
Мок_ВозвращаемыеЗначения.Колонки.Добавить("Мок_ВозвращаемоеЗначение");

Мок_РежимУстановкиВозвращаемогоЗначенияМетода = Ложь;
